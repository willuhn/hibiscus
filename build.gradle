plugins {
    id 'java'
    id 'eclipse' // optional (to generate Eclipse project files)
    id 'idea' // optional (to generate IntelliJ IDEA project files)
}
apply from: "${rootProject.rootDir}/url_file.gradle"

group 'de.willuhn.jameica'
version '2.10.4-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'ISO-8859-1'
}
tasks.withType(Javadoc) {
    options.encoding = 'ISO-8859-1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lib:util')
    implementation project(':lib:datasource')
    implementation project(':jameica')

    implementation libs.db.postgres
    implementation libs.nanoxml
    implementation libs.guava
    implementation libs.hbci4j
    implementation libs.itext
    implementation libs.konik
    implementation libs.apache.commons.lang
    implementation libs.apache.velocity
    implementation libs.supercsv
    implementation swt.platform

    // see <https://projects.eclipse.org/projects/science.swtchart>
    implementation urlFile('swtchart_0.13.0', 'https://download.eclipse.org/swtchart/releases/0.13.0/repository/plugins/org.eclipse.swtchart_0.13.0.202009151159.jar')
    implementation urlFile('swtchart.extensions_0.13.0', 'https://download.eclipse.org/swtchart/releases/0.13.0/repository/plugins/org.eclipse.swtchart.extensions_0.13.0.202009151159.jar')
    // Printing plug-in for SWT. See <https://code.google.com/archive/p/swt-paperclips/> (last update: 2009-08-12)
    implementation urlFile('paperclips-1.0.4', 'https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/swt-paperclips/net.sf.paperclips_1.0.4.200908120926.jar')
    // see <https://sourceforge.net/projects/obantoo/> (last update: 2015-05-10)
    // new version (Java13+) see <https://github.com/jverein/obantoo2>
    implementation urlFile('obantoo-bin-2.1.12', 'https://downloads.sourceforge.net/project/obantoo/obantoo-bin-2.1.12.jar')

    testImplementation libs.junit
}

// solange noch die Logik des ANT-Builds ben√∂tigt wird, nutze einen Unterordner
buildDir = file("./build/gradle/")
jar {
    baseName "de_willuhn_${project.name}"
    manifest {
        attributes "Main-Class": "de.willuhn.jameica.Main"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = false
    }
}
idea {
    module {
        downloadJavadoc = true
        downloadSources = false
    }
}
