plugins {
    id 'java'
    id 'eclipse' // optional (to generate Eclipse project files)
    id 'idea' // optional (to generate IntelliJ IDEA project files)
}
apply from: "${rootProject.rootDir}/eclipse_swt.gradle"
apply from: "${rootProject.rootDir}/url_file.gradle"

group 'de.willuhn.jameica'
version '2.10.4-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'ISO-8859-1'
}
tasks.withType(Javadoc) {
    options.encoding = 'ISO-8859-1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lib:util')
    implementation project(':lib:datasource')
    implementation project(':jameica')

    implementation group: 'be.cyberelf.nanoxml', name: 'nanoxml', version: '2.2.3' // (last update: 2008-06-16)
    implementation group: 'com.google.guava', name: 'guava', version: '25.1-jre'
    implementation group: 'com.github.hbci4j', name: 'hbci4j-core', version: '3.1.55'
    implementation group: 'com.itextpdf', name:'itext-pdfa', version: '5.5.2'
    implementation group: 'io.konik', name: 'konik', version: '1.3.2'
    implementation group: 'net.sf.supercsv', name: 'super-csv-java8', version: '2.4.0'
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
    //implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0' //see <https://stackoverflow.com/a/25193935>
    implementation group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    implementation group: 'org.eclipse.platform', name: 'org.eclipse.swt.'+swtPlatform, version: swtVersion
    implementation group: 'postgresql', name: 'postgresql', version: '8.3-606.jdbc3'

    // see <https://projects.eclipse.org/projects/science.swtchart>
    implementation urlFile('swtchart_0.13.0', 'https://download.eclipse.org/swtchart/releases/0.13.0/repository/plugins/org.eclipse.swtchart_0.13.0.202009151159.jar')
    implementation urlFile('swtchart.extensions_0.13.0', 'https://download.eclipse.org/swtchart/releases/0.13.0/repository/plugins/org.eclipse.swtchart.extensions_0.13.0.202009151159.jar')
    // Printing plug-in for SWT. See <https://code.google.com/archive/p/swt-paperclips/> (last update: 2009-08-12)
    implementation urlFile('paperclips-1.0.4', 'https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/swt-paperclips/net.sf.paperclips_1.0.4.200908120926.jar')
    // see <https://sourceforge.net/projects/obantoo/> (last update: 2015-05-10)
    // new version (Java13+) see <https://github.com/jverein/obantoo2>
    implementation urlFile('obantoo-bin-2.1.12', 'https://downloads.sourceforge.net/project/obantoo/obantoo-bin-2.1.12.jar')

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}

// solange noch die Logik des ANT-Builds ben√∂tigt wird, nutze einen Unterordner
buildDir = file("./build/gradle/")
jar {
    baseName "de_willuhn_${project.name}"
    manifest {
        attributes "Main-Class": "de.willuhn.jameica.Main"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = false
    }
}
idea {
    module {
        downloadJavadoc = true
        downloadSources = false
    }
}
