plugins {
    id 'my.java-library-conventions'
    id 'my.java-oldschool-project-structure'
    id 'my.encoding.8859-1'
}
apply from: "${rootProject.rootDir}/gradle/url_file.gradle"

group 'de.willuhn.jameica'
version '2.10.4-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    // Dependencies appearing in the api configurations will be transitively
    // exposed to consumers of the library, and as such will appear on the
    // compile classpath of consumers.
    api project(':lib:util')
    api project(':lib:datasource')
    api project(':jameica')

    implementation libs.db.postgres
    implementation libs.nanoxml
    implementation libs.guava
    implementation libs.hbci4j
    implementation libs.itext
    implementation libs.konik
    implementation libs.apache.commons.lang
    implementation libs.apache.velocity
    implementation libs.supercsv
    implementation swt.platform

    // see <https://projects.eclipse.org/projects/science.swtchart>
    implementation urlFile('swtchart_0.13.0', 'https://download.eclipse.org/swtchart/releases/0.13.0/repository/plugins/org.eclipse.swtchart_0.13.0.202009151159.jar')
    implementation urlFile('swtchart.extensions_0.13.0', 'https://download.eclipse.org/swtchart/releases/0.13.0/repository/plugins/org.eclipse.swtchart.extensions_0.13.0.202009151159.jar')
    // Printing plug-in for SWT. See <https://code.google.com/archive/p/swt-paperclips/> (last update: 2009-08-12)
    implementation urlFile('paperclips-1.0.4', 'https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/swt-paperclips/net.sf.paperclips_1.0.4.200908120926.jar')
    // see <https://sourceforge.net/projects/obantoo/> (last update: 2015-05-10)
    // new version (Java13+) see <https://github.com/jverein/obantoo2>
    implementation urlFile('obantoo-bin-2.1.12', 'https://downloads.sourceforge.net/project/obantoo/obantoo-bin-2.1.12.jar')
}

// solange noch die Logik des ANT-Builds ben√∂tigt wird, nutze einen Unterordner
buildDir = file("./build/gradle/")
jar {
    baseName "de_willuhn_${project.name}"
    manifest {
        attributes "Main-Class": "de.willuhn.jameica.Main"
    }
}
